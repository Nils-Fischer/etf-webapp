///////////////////////////////////////////////////////////////////////////////////////
//
// ETF-WebApp
//
///////////////////////////////////////////////////////////////////////////////////////

group = 'de.interactive_instruments.etf'
description = 'ETF Presentation and Controller Layer'

apply plugin: 'war'

ext.springVersion = "4.3.28.RELEASE"
ext.jacksonVersion = "2.13.2"
ext.springfoxSwaggerVersion = "2.9.2"

configurations {
    compile.exclude group:'xalan'
}

// http://docs.spring.io/spring-boot/docs/current/reference/html/howto-hotswapping.html
idea {
    module {
        inheritOutputDirs = false
        outputDir = file("$buildDir/classes/main/")
    }
}

dependencies {

    compile group: 'net.sf.saxon', name: 'Saxon-HE', version: '9.7.0-21'

    implementation project(':etf-core')
    implementation project(':etf-spi')
    implementation project(':etf-stdtot')
    implementation project(':etf-bsxds')

    // Validation
    compile group: 'org.glassfish', name: 'javax.el', version: '3.0.1-b11'
    compile group: 'org.hibernate', name: 'hibernate-validator', version: '5.4.3.Final'


    compile("xerces:xercesImpl:"+etf_xercesVersion){
        force = true
    }
    compile("xml-apis:xml-apis:"+etf_xmlApisVersion){
        force = true
    }

    compile group: 'commons-logging', name: 'commons-logging', version:'1.1.1'
    compile group: 'commons-codec', name: 'commons-codec', version: etf_commonsCodecVersion
    compile group: 'commons-fileupload', name: 'commons-fileupload', version:'1.3.3'
    compile group: 'commons-io', name: 'commons-io', version: etf_commonsIoVersion
    compile group: 'commons-lang', name: 'commons-lang', version:'2.6'

    // align with gradle
    compile localGroovy()

    compile group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'

    compile group: 'org.jsoup', name: 'jsoup', version: '1.14.2'

    compile group: 'org.slf4j', name: 'slf4j-api', version: etf_slf4jApiVersion

    // Conditional logback config file processing
    compile group: 'org.codehaus.janino', name: 'janino', version: '2.7.8'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: etf_logbackVersion


	// Spring dependencies
	//////////////////////////////////

    // JSON
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: jacksonVersion
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jacksonVersion
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: jacksonVersion

    compile(group: 'org.springframework', name: 'spring-webmvc', version: springVersion ) {
        exclude(module: 'commons-logging')
    }

    // Include nekohtml for thymeleaf
    compile('net.sourceforge.nekohtml:nekohtml:1.9.22') {
        exclude group: 'xerces', module: 'xercesImpl'
    }

    compile "javax.validation:validation-api:1.1.0.Final",
            "org.thymeleaf:thymeleaf-spring4:2.1.6.RELEASE",
            "org.thymeleaf.extras:thymeleaf-extras-conditionalcomments:2.1.2.RELEASE"

    compile("org.thymeleaf.extras:thymeleaf-extras-tiles2-spring4:2.1.1.RELEASE") {
        exclude group: "org.slf4j", module: "jcl-over-slf4j"
    }

    compile group: 'javax.activation', name: 'activation', version: etf_activationVersion
	compile group: 'javax.xml.bind', name: 'jaxb-api', version: etf_jaxbVersion
	compile group: 'org.glassfish.jaxb', name: 'jaxb-runtime', version: etf_jaxbVersion

    providedCompile group: 'javax.servlet', name: 'javax.servlet-api', version:'4.0.1'


    providedCompile group: 'javax.servlet.jsp', name: 'jsp-api', version:'2.2'
    compile group: 'javax.servlet', name: 'jstl', version:'1.2'
    compile group: 'joda-time', name: 'joda-time', version:'2.9.9'
    runtime group: 'joda-time', name: 'joda-time-jsptags', version:'1.1.1'

    // Testing
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: etf_junitTestVersion
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: etf_junitTestVersion
    testCompile group: 'de.interactive_instruments.etf.test', name: 'etf-unittest', version:'1.0.0'+project.snapshotSuffix
    testCompile('org.seleniumhq.selenium:selenium-java:4.1.3')
    testCompile('org.seleniumhq.selenium:selenium-api:4.1.3')
    testCompile('io.github.bonigarcia:webdrivermanager:5.1.0')

    // API
    compile group: 'io.springfox', name: 'springfox-swagger-ui', version: springfoxSwaggerVersion, {
        exclude module: 'swagger-annotations'
        exclude module: 'swagger-models'
    }
    compile group: 'io.springfox', name: 'springfox-swagger2', version: springfoxSwaggerVersion
    compile("io.swagger:swagger-annotations:1.5.22")
    compile("io.swagger:swagger-models:1.5.22")

    // Upgrade Thymeleaf dependency, see https://nvd.nist.gov/vuln/detail/CVE-2016-3093
    compile group: 'ognl', name: 'ognl', version: '3.0.21'

    // Upgrade Thymeleaf, Tiles dependency, see https://nvd.nist.gov/vuln/detail/CVE-2014-0114
    compile group: 'commons-beanutils', name: 'commons-beanutils', version: '1.9.4'
}

war {
    doFirst {
        manifest {
            attributes(
                    'Implementation-Title': project.name,
                    'Implementation-Vendor': 'interactive instruments GmbH',
                    'Implementation-Vendor-Id': 'de.interactive_instruments',
                    'Implementation-Version': project.version,
                    'Built-By': System.getProperty('user.name'),
                    'Build-Host': java.net.InetAddress.getLocalHost().getHostName(),
                    'Build-JDK': System.getProperty('java.version'),
                    'Build-Time': new Date().format("yyyyMMdd'T'HHmm"),
                    'Source-Compatibility': project.sourceCompatibility,
                    'Target-Compatibility': project.targetCompatibility
            )
        }

        archiveName 'etf-webapp.war'
    }
}

repositories {
    mavenCentral()
}
